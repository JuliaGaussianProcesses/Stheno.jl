<!DOCTYPE html>
<html lang="en"><head><meta charset="UTF-8"/><meta name="viewport" content="width=device-width, initial-scale=1.0"/><title>CO2 and temperature · Stheno.jl</title><script data-outdated-warner src="../../assets/warner.js"></script><link href="https://cdnjs.cloudflare.com/ajax/libs/lato-font/3.0.0/css/lato-font.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/juliamono/0.039/juliamono-regular.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.3/css/fontawesome.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.3/css/solid.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.3/css/brands.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/KaTeX/0.13.11/katex.min.css" rel="stylesheet" type="text/css"/><script>documenterBaseURL="../.."</script><script src="https://cdnjs.cloudflare.com/ajax/libs/require.js/2.3.6/require.min.js" data-main="../../assets/documenter.js"></script><script src="../../siteinfo.js"></script><script src="../../../versions.js"></script><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/documenter-dark.css" data-theme-name="documenter-dark" data-theme-primary-dark/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/documenter-light.css" data-theme-name="documenter-light" data-theme-primary/><script src="../../assets/themeswap.js"></script></head><body><div id="documenter"><nav class="docs-sidebar"><div class="docs-package-name"><span class="docs-autofit"><a href="../../">Stheno.jl</a></span></div><form class="docs-search" action="../../search/"><input class="docs-search-query" id="documenter-search-query" name="q" type="text" placeholder="Search docs"/></form><ul class="docs-menu"><li><a class="tocitem" href="../../">Home</a></li><li><a class="tocitem" href="../getting_started/">Getting Started</a></li><li><a class="tocitem" href="../../input_types/">Input Types</a></li><li><a class="tocitem" href="../../kernel_design/">Kernel Design</a></li><li><a class="tocitem" href="../../internals/">Internals</a></li><li><a class="tocitem" href="../custom_affine_transformations/">Custom Affine Transformations</a></li><li><a class="tocitem" href="../../api/">API</a></li><li><span class="tocitem">Examples</span><ul><li><a class="tocitem" href="../../examples_note/">Notes on Examples</a></li><li><a class="tocitem" href="../process_decomposition/">Process Decomposition</a></li><li><a class="tocitem" href="../sensor_fusion/">Sensor Fusion</a></li><li><a class="tocitem" href="../time_varying_blr/">Time-Varying Bayesian Linear Regression</a></li><li><a class="tocitem" href="../gppp_and_pseudo_points/">Pseudo-Points in Additive Regression</a></li><li><a class="tocitem" href="../differentiation/">Differentation</a></li><li class="is-active"><a class="tocitem" href>CO2 and temperature</a><ul class="internal"><li><a class="tocitem" href="#Obtain-the-data"><span>Obtain the data</span></a></li><li><a class="tocitem" href="#Build-and-train-a-model-for-the-data"><span>Build and train a model for the data</span></a></li><li><a class="tocitem" href="#Plot-the-resulting-model-fit."><span>Plot the resulting model fit.</span></a></li></ul></li></ul></li></ul><div class="docs-version-selector field has-addons"><div class="control"><span class="docs-label button is-static is-size-7">Version</span></div><div class="docs-selector control is-expanded"><div class="select is-fullwidth is-size-7"><select id="documenter-version-selector"></select></div></div></div></nav><div class="docs-main"><header class="docs-navbar"><nav class="breadcrumb"><ul class="is-hidden-mobile"><li><a class="is-disabled">Examples</a></li><li class="is-active"><a href>CO2 and temperature</a></li></ul><ul class="is-hidden-tablet"><li class="is-active"><a href>CO2 and temperature</a></li></ul></nav><div class="docs-right"><a class="docs-edit-link" href="https://github.com/JuliaGaussianProcesses/Stheno.jl/blob/master/examples/extended_mauna_loa/script.jl" title="Edit on GitHub"><span class="docs-icon fab"></span><span class="docs-label is-hidden-touch">Edit on GitHub</span></a><a class="docs-settings-button fas fa-cog" id="documenter-settings-button" href="#" title="Settings"></a><a class="docs-sidebar-button fa fa-bars is-hidden-desktop" id="documenter-sidebar-button" href="#"></a></div></header><article class="content" id="documenter-page"><h1 id="CO2-and-temperature"><a class="docs-heading-anchor" href="#CO2-and-temperature">CO2 and temperature</a><a id="CO2-and-temperature-1"></a><a class="docs-heading-anchor-permalink" href="#CO2-and-temperature" title="Permalink"></a></h1><p><a href="https://nbviewer.jupyter.org/github/JuliaGaussianProcesses/Stheno.jl/blob/gh-pages/dev/examples/extended_mauna_loa.ipynb"><img src="https://img.shields.io/badge/show-nbviewer-579ACA.svg" alt/></a></p><p><em>You are seeing the HTML output generated by <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> and <a href="https://github.com/fredrikekre/Literate.jl">Literate.jl</a> from the <a href="https://github.com/JuliaGaussianProcesses/Stheno.jl/blob/master/examples/extended_mauna_loa/script.jl">Julia source file</a>. The corresponding notebook can be viewed in <a href="https://nbviewer.jupyter.org/github/JuliaGaussianProcesses/Stheno.jl/blob/gh-pages/dev/examples/extended_mauna_loa.ipynb">nbviewer</a>.</em></p><pre><code class="language-julia hljs">using AbstractGPs
using AbstractGPsMakie
using CairoMakie
using CSV
using DataDeps
using DataFrames
using Dates
using Distributions
using KernelFunctions
using LinearAlgebra
using Optim
using ParameterHandling
using Random
using Statistics
using Stheno
using Zygote

using CairoMakie: RGB

ENV[&quot;DATADEPS_ALWAYS_ACCEPT&quot;] = &quot;true&quot;</code></pre><pre><code class="nohighlight hljs">&quot;true&quot;</code></pre><h2 id="Obtain-the-data"><a class="docs-heading-anchor" href="#Obtain-the-data">Obtain the data</a><a id="Obtain-the-data-1"></a><a class="docs-heading-anchor-permalink" href="#Obtain-the-data" title="Permalink"></a></h2><p>Use DataDeps.jl to obtain the data.</p><pre><code class="language-julia hljs">register(DataDep(
    &quot;mauna_loa&quot;,
    &quot;The classic Mauna Loa CO2 data.&quot;,
    &quot;https://scrippsco2.ucsd.edu/assets/data/atmospheric/stations/in_situ_co2/monthly/&quot; *
        &quot;monthly_in_situ_co2_mlo.csv&quot;,
))
register(DataDep(
    &quot;hadcrut5_global&quot;,
    &quot;Globally-averaged surface temperatures&quot;,
    &quot;https://www.metoffice.gov.uk/hadobs/hadcrut5/data/current/analysis/diagnostics/&quot; *
        &quot;HadCRUT.5.0.1.0.analysis.summary_series.global.monthly.csv&quot;,
))</code></pre><pre><code class="nohighlight hljs">DataDeps.DataDep{Nothing, String, typeof(DataDeps.fetch_default), typeof(identity)}(&quot;hadcrut5_global&quot;, &quot;https://www.metoffice.gov.uk/hadobs/hadcrut5/data/current/analysis/diagnostics/HadCRUT.5.0.1.0.analysis.summary_series.global.monthly.csv&quot;, nothing, DataDeps.fetch_default, identity, &quot;Globally-averaged surface temperatures&quot;)</code></pre><p>Load and pre-process the data.</p><pre><code class="language-julia hljs">ml_df, Ttr_df, Tte_df = let

    mauna_loa_co2 = let
        mauna_loa_data_raw = CSV.read(
            joinpath(datadep&quot;mauna_loa&quot;, &quot;monthly_in_situ_co2_mlo.csv&quot;), DataFrame;
            skipto=58, header=false,
        )

        data = DataFrame(
            date=map(Date, mauna_loa_data_raw.Column1, mauna_loa_data_raw.Column2),
            CO2=map(x -&gt; x == -99.99 ? missing : x, mauna_loa_data_raw.Column5),
        )
        data = filter(:CO2 =&gt; !ismissing, data)

        # standardise CO2 data to have unit mean.
        m = mean(data.CO2)
        σ = std(data.CO2)
        data.CO2 = Float64.(data.CO2 .- m) ./ σ

        # Convert dates into floats.
        data.float_date = map(x -&gt; x.value, data.date .- Date(1)) ./ 365

        data
    end

    temperature = let
        hadcrut5_data_raw = CSV.read(
            joinpath(
                datadep&quot;hadcrut5_global&quot;,
                &quot;HadCRUT.5.0.1.0.analysis.summary_series.global.monthly.csv&quot;,
            ),
            DataFrame;
            header=1,
        )

        tmp = DataFrame(
            date=hadcrut5_data_raw[!, &quot;Time&quot;],
            temperature=hadcrut5_data_raw[!, &quot;Anomaly (deg C)&quot;],
        )

        # Convert dates into floats.
        tmp.float_date = map(x -&gt; x.value, tmp.date .- Date(1)) ./ 365

        # Remove any data from before the first Mauna Loa data.
        time_min = minimum(mauna_loa_co2.date)
        filter(:date =&gt; date -&gt; date &gt;= time_min, tmp)
    end

    # Split temperature data into train and test.
    Ntr = 425
    Ttr_raw = temperature[1:Ntr, :]
    Tte_raw = temperature[(Ntr + 1):end, :]

    # Standard temperature data using training data.
    mtr = mean(Ttr_raw.temperature)
    σtr = std(Ttr_raw.temperature)
    Ttr = DataFrame(
        date=Ttr_raw.date,
        float_date=Ttr_raw.float_date,
        temperature=(Ttr_raw.temperature .- mtr) ./ σtr,
    )
    Tte = DataFrame(
        date=Tte_raw.date,
        float_date=Tte_raw.float_date,
        temperature=(Tte_raw.temperature .- mtr) ./ σtr,
    )

    mauna_loa_co2, Ttr, Tte
end;</code></pre><pre><code class="nohighlight hljs">┌ Warning: Checksum not provided, add to the Datadep Registration the following hash line
│   hash = &quot;bbeb66a24684e96ddd86bc5942dd2e444785fb0908fa9f2a5b50126ff2151575&quot;
└ @ DataDeps ~/.julia/packages/DataDeps/ooWXe/src/verification.jl:44
┌ Warning: Checksum not provided, add to the Datadep Registration the following hash line
│   hash = &quot;c3590af7d9067f1097b7254fcc0735ba068c5efeb43f36a0982b983b75961aab&quot;
└ @ DataDeps ~/.julia/packages/DataDeps/ooWXe/src/verification.jl:44
</code></pre><h2 id="Build-and-train-a-model-for-the-data"><a class="docs-heading-anchor" href="#Build-and-train-a-model-for-the-data">Build and train a model for the data</a><a id="Build-and-train-a-model-for-the-data-1"></a><a class="docs-heading-anchor-permalink" href="#Build-and-train-a-model-for-the-data" title="Permalink"></a></h2><p>Model for CO2 and temperature as a function of time.</p><pre><code class="language-julia hljs">function build_gp(θ)
    return @gppp let

        # Shared trend process.
        f_trend = stretch(GP(SEKernel()), θ.λ_trend)

        # Specify model for CO2.
        f_co2_latent = θ.CO2.σ_latent * f_trend
        f_co2_wiggle = θ.CO2.σ_wiggle * stretch(GP(SEKernel()), θ.CO2.λ_wiggle)
        f_co2_period = θ.CO2.σ_period * GP(SEKernel() ∘ PeriodicTransform(θ.CO2.freq))
        f_co2 = f_co2_latent + f_co2_wiggle + f_co2_period + θ.CO2.σm * GP(ConstantKernel())

        # Specify model for temperature.
        f_T_trend = θ.T.σ_trend * f_trend
        f_T_wiggle = θ.T.σ_wiggle * stretch(GP(SEKernel()), θ.T.λ_wiggle)
        f_T = f_T_trend + f_T_wiggle + θ.T.σm * GP(ConstantKernel())
    end
end

x_co2 = ml_df.float_date;
x_T_tr = Ttr_df.float_date;
x_T_te = Tte_df.float_date;

y_co2 = ml_df.CO2;
y_T_tr = Ttr_df.temperature;
y_T_te = Tte_df.temperature;

x = BlockData(GPPPInput(:f_co2, x_co2), GPPPInput(:f_T, x_T_tr))
y = vcat(y_co2, y_T_tr)

function build_obs_cov(θ)
    σ²_CO2 = θ.CO2.σ_noise^2
    σ²_T = θ.T.σ_noise^2
    return Diagonal(vcat(fill(σ²_CO2, length(y_co2)), fill(σ²_T, length(y_T_tr))))
end

function nlml(θ)
    f = build_gp(θ)
    return -logpdf(f(x, build_obs_cov(θ)), y)
end

init_params = (
    λ_trend = positive(1e-1),
    CO2 = (
        σ_latent = positive(0.3),
        σ_wiggle = positive(0.3),
        λ_wiggle = positive(5.0),
        σ_period = positive(0.3),
        freq = fixed(1.0),
        σ_noise = positive(0.1),
        σm = positive(1.0),
    ),
    T = (
        σ_trend = positive(1.0),
        σ_wiggle = positive(1.0),
        λ_wiggle = positive(0.5),
        σ_noise = positive(0.1),
        σm = positive(1.0),
    ),
)

default_optimizer = LBFGS(;
    alphaguess=Optim.LineSearches.InitialStatic(; scaled=true),
    linesearch=Optim.LineSearches.BackTracking(),
)

function optimize_loss(loss, θ_init; optimizer=default_optimizer, maxiter=1_000)
    options = Optim.Options(; iterations=maxiter, show_trace=true)

    θ_flat_init, unflatten = ParameterHandling.value_flatten(θ_init)
    loss_packed = loss ∘ unflatten

    # https://julianlsolvers.github.io/Optim.jl/stable/#user/tipsandtricks/#avoid-repeating-computations
    function fg!(F, G, x)
        if F !== nothing &amp;&amp; G !== nothing
            val, grad = Zygote.withgradient(loss_packed, x)
            G .= only(grad)
            return val
        elseif G !== nothing
            grad = Zygote.gradient(loss_packed, x)
            G .= only(grad)
            return nothing
        elseif F !== nothing
            return loss_packed(x)
        end
    end

    result = optimize(Optim.only_fg!(fg!), θ_flat_init, optimizer, options; inplace=false)

    return unflatten(result.minimizer), result
end

θ_opt, result = optimize_loss(nlml, init_params)</code></pre><pre><code class="nohighlight hljs">((λ_trend = 0.08117925596820141, CO2 = (σ_latent = 1.0929694377096424, σ_wiggle = 0.013705932051262778, λ_wiggle = 5.326764962974475, σ_period = 0.06066085696206938, freq = 1.0, σ_noise = 0.007310148041717022, σm = 0.005211200364913826), T = (σ_trend = 1.6624938593141776, σ_wiggle = 0.5791619163697767, λ_wiggle = 3.6335576216944028, σ_noise = 0.40366881695460455, σm = 1.1786155793352608)),  * Status: success

 * Candidate solution
    Final objective value:     -1.952912e+03

 * Found with
    Algorithm:     L-BFGS

 * Convergence measures
    |x - x&#39;|               = 0.00e+00 ≤ 0.0e+00
    |x - x&#39;|/|x&#39;|          = 0.00e+00 ≤ 0.0e+00
    |f(x) - f(x&#39;)|         = 0.00e+00 ≤ 0.0e+00
    |f(x) - f(x&#39;)|/|f(x&#39;)| = 0.00e+00 ≤ 0.0e+00
    |g(x)|                 = 4.50e-02 ≰ 1.0e-08

 * Work counters
    Seconds run:   284  (vs limit Inf)
    Iterations:    78
    f(x) calls:    101
    ∇f(x) calls:   78
)</code></pre><h2 id="Plot-the-resulting-model-fit."><a class="docs-heading-anchor" href="#Plot-the-resulting-model-fit.">Plot the resulting model fit.</a><a id="Plot-the-resulting-model-fit.-1"></a><a class="docs-heading-anchor-permalink" href="#Plot-the-resulting-model-fit." title="Permalink"></a></h2><pre><code class="language-julia hljs">function colours()
    return Dict(
        :blue =&gt; RGB(0/255, 107/255, 164/255),
        :cyan =&gt; RGB(75/255, 166/255, 251/255),
        :red =&gt; RGB(200/255, 82 / 255, 0 / 255),
        :pink =&gt; RGB(169/255, 90/255, 161/255),
        :black =&gt; RGB(0.0, 0.0, 0.0),
        :orange =&gt; RGB(245/255, 121/255, 58/255),
    )
end

band_alpha() = 0.3
sample_alpha() = 0.2
point_alpha() = 1.0

x_plot = range(
    minimum(vcat(ml_df.float_date, Ttr_df.float_date)),
    maximum(vcat(ml_df.float_date, Tte_df.float_date));
    length=1_500,
);

f_post = posterior(build_gp(θ_opt)(x, build_obs_cov(θ_opt)), y);

function plot_band!(ax, x_plot, fx, colour, label)
    ms = marginals(fx)
    symband!(
        ax, x_plot, mean.(ms), std.(ms);
        bandscale=3,
        color=(colours()[colour], 0.5 * band_alpha()),
        label=label,
    )
end

function plot_sample!(ax, x_plot, fx, colour)
    gpsample!(
        ax, x_plot, fx;
        samples=4, color=(colours()[colour], sample_alpha()),
    )
end

function plot_gp!(ax, x_plot, fx, colour, label)
    plot_band!(ax, x_plot, fx, colour, label)
    plot_sample!(ax, x_plot, fx, colour)
end

let
    fig = Figure()
    ga = fig[1, 1] = GridLayout()

    jitter = 1e-6

    # Plot CO2 posterior.
    let
        ax = Axis(ga[1, 1])
        f_post_x = f_post(GPPPInput(:f_co2, x_plot), jitter)

        plot_band!(ax, x_plot, f_post(GPPPInput(:f_co2_latent, x_plot), jitter), :orange, &quot;CO2 (latent)&quot;)
        plot_band!(ax, x_plot, f_post(GPPPInput(:f_co2_wiggle, x_plot), jitter), :black, &quot;CO2 (wiggle)&quot;)
        plot_band!(ax, x_plot, f_post(GPPPInput(:f_co2_period, x_plot), jitter), :pink, &quot;CO2 (period)&quot;)

        plot_gp!(ax, x_plot, f_post_x, :blue, &quot;CO2&quot;)
        scatter!(ax, x_co2, y_co2; color=(colours()[:blue], point_alpha()), markersize=3)

        axislegend(ax; position=:lt)

        hidexdecorations!(ax; grid=false)
    end

    # Plot temperature posterior.
    let
        ax = Axis(ga[2, 1])
        f_post_x = f_post(GPPPInput(:f_T, x_plot), jitter)

        plot_band!(ax, x_plot, f_post(GPPPInput(:f_T_trend, x_plot), jitter), :orange, &quot;T (trend)&quot;)
        plot_band!(ax, x_plot, f_post(GPPPInput(:f_T_wiggle, x_plot), jitter), :black, &quot;T (wiggle)&quot;)

        plot_gp!(ax, x_plot, f_post_x, :blue, &quot;T&quot;)
        scatter!(ax, x_T_tr, y_T_tr; color=(colours()[:blue], point_alpha()), markersize=3)
        scatter!(
            ax, x_T_te, y_T_te;
            color=(colours()[:pink], point_alpha()),
            markersize=5,
            marker=:xcross,
            label=&quot;T (test)&quot;
        )

        axislegend(ax; position=:lt)
    end

    rowgap!(ga, 5)

    fig
end</code></pre><p><img src="../3240894250.png" alt/></p><hr/><p><em>This page was generated using <a href="https://github.com/fredrikekre/Literate.jl">Literate.jl</a>.</em></p></article><nav class="docs-footer"><a class="docs-footer-prevpage" href="../differentiation/">« Differentation</a><div class="flexbox-break"></div><p class="footer-message">Powered by <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> and the <a href="https://julialang.org/">Julia Programming Language</a>.</p></nav></div><div class="modal" id="documenter-settings"><div class="modal-background"></div><div class="modal-card"><header class="modal-card-head"><p class="modal-card-title">Settings</p><button class="delete"></button></header><section class="modal-card-body"><p><label class="label">Theme</label><div class="select"><select id="documenter-themepicker"><option value="documenter-light">documenter-light</option><option value="documenter-dark">documenter-dark</option></select></div></p><hr/><p>This document was generated with <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> version 0.27.10 on <span class="colophon-date" title="Wednesday 12 January 2022 21:51">Wednesday 12 January 2022</span>. Using Julia version 1.7.1.</p></section><footer class="modal-card-foot"></footer></div></div></div></body></html>
