{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Custom Affine Transformations\n",
    "\n",
    "*You are seeing the\n",
    "notebook output generated by\n",
    "[Literate.jl](https://github.com/fredrikekre/Literate.jl) from the\n",
    "[Julia source file](https://github.com/JuliaGaussianProcesses/Stheno.jl/blob/master/examples/custom_affine_transformations/script.jl).\n",
    "The rendered HTML can be viewed [in the docs](https://juliagaussianprocesses.github.io/KernelFunctions.jl/dev/examples/custom_affine_transformations/).*"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "This page explains how to implement your own affine transformation operations by way of\n",
    "example.\n",
    "You can add your own custom affine transformations into Stheno using the same\n",
    "mechanism as all of the existing transformations (addition, multiplication, composition,\n",
    "etc).\n",
    "First, load up the relevant packages."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using AbstractGPs\n",
    "using LinearAlgebra\n",
    "using Stheno"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "## The Affine Transformation"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "Suppose that, for some reason, you wish to implement the affine transformation of a single\n",
    "process `f` given by `(Af)(x) = f(x) + f(x + 3) - 2`.\n",
    "In order to define this transformation, first create a function which accepts `f` and\n",
    "returns a `CompositeGP`:"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "A (generic function with 1 method)"
     },
     "metadata": {},
     "execution_count": 2
    }
   ],
   "cell_type": "code",
   "source": [
    "using Stheno: AbstractGP, CompositeGP, SthenoAbstractGP\n",
    "\n",
    "A(f::SthenoAbstractGP) = CompositeGP((A, f), f.gpc)"
   ],
   "metadata": {},
   "execution_count": 2
  },
  {
   "cell_type": "markdown",
   "source": [
    "The first argument to `CompositeGP` contains `A` itself and any data needed to fully\n",
    "specify the process results from this transformation. In this case the only piece of\n",
    "information required is `f`, but really any data can be put in this argument.\n",
    "For example, if we wished to replace the translation of `-3` by a parameter, we could do\n",
    "so, and make it a part of this first argument.\n",
    "\n",
    "The second argument is the book-keeping object that needs to be passed around in order to\n",
    "know how to compute covariances properly.\n",
    "It is because of this argument that we restrict the accepted GPs to be `SthenoAbstractGP`,\n",
    "as we can safely assume that these have a `gpc` field."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "We'll now define a type alias in order to simplify some methods later on:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "const A_args = Tuple{typeof(A), SthenoAbstractGP};"
   ],
   "metadata": {},
   "execution_count": 3
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Most Important Methods"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "We must now define methods of three functions on\n",
    "`A_args`: `mean`, `cov`, and `var`.\n",
    "First the `mean` -- this method should accept both an `A_args` and an `AbstracVector`, and\n",
    "return the mean vector of `A(f)` at `x`.\n",
    "Some textbook calculations reveal that this is"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "Stheno.mean((A, f)::A_args, x::AbstractVector) = mean(f, x) .+ mean(f, x .+ 3) .- 2"
   ],
   "metadata": {},
   "execution_count": 4
  },
  {
   "cell_type": "markdown",
   "source": [
    "The first argument here is _always_ going to be precisely the tuple of arguments passed\n",
    "into the `CompositeGP` constructor above.\n",
    "You can assume that you can compute any statistics of `f` that the AbstractGPs API\n",
    "provides."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "We now turn our attention to `cov`. The first method we consider is\n",
    "`cov(args::A_args, x::AbstractVector, y::AbstractVector)`, which should return the\n",
    "cross-covariance matrix between all pairs of points in `x` and `y` under the transformed\n",
    "process, `A(f)`.\n",
    "Again, some standard manipulations reveal that this covariance is given by"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function Stheno.cov((A, f)::A_args, x::AbstractVector, y::AbstractVector)\n",
    "    return cov(f, x, y) + cov(f, x, y .+ 3) + cov(f, x .+ 3, y) + cov(f, x .+ 3, y .+ 3)\n",
    "end"
   ],
   "metadata": {},
   "execution_count": 5
  },
  {
   "cell_type": "markdown",
   "source": [
    "The last substantially new method to implement is\n",
    "`cov(args::A_args, g::AbstractGP, x::AbstractVector, y::AbstractVector)`, which should\n",
    "return the cross-covariance matrix between `A(f)` at `x` and `g` at `y`.\n",
    "When implementing this method, you can assume you have access to functions like\n",
    "`cov(f, g, x, y)` etc:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function Stheno.cov((A, f)::A_args, g::AbstractGP, x::AbstractVector, y::AbstractVector)\n",
    "    return cov(f, g, x, y) + cov(f, g, x .+ 3, y)\n",
    "end"
   ],
   "metadata": {},
   "execution_count": 6
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Additional (Required) Methods"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "There are a number of other methods that you should implement.\n",
    "These are all just special cases or slight modifications of the three methods above, and\n",
    "should be straightforward to implement given that you've implemented the above methods."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "First, lets build a GPPP containing an instance of our transformation so that some\n",
    "properties can be verified.\n",
    "The definition of the methods being implemented is demonstrated by checking an\n",
    "equality after defining each method."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "Stheno.GaussianProcessProbabilisticProgramme{NamedTuple{(:f, :Af), Tuple{Stheno.WrappedGP{AbstractGPs.GP{AbstractGPs.ZeroMean{Float64}, KernelFunctions.SqExponentialKernel{Distances.Euclidean}}}, Stheno.CompositeGP{Tuple{typeof(Main.##306.A), Stheno.WrappedGP{AbstractGPs.GP{AbstractGPs.ZeroMean{Float64}, KernelFunctions.SqExponentialKernel{Distances.Euclidean}}}}}}}}((f = Stheno.WrappedGP{AbstractGPs.GP{AbstractGPs.ZeroMean{Float64}, KernelFunctions.SqExponentialKernel{Distances.Euclidean}}}(AbstractGPs.GP{AbstractGPs.ZeroMean{Float64}, KernelFunctions.SqExponentialKernel{Distances.Euclidean}}(AbstractGPs.ZeroMean{Float64}(), Squared Exponential Kernel (metric = Distances.Euclidean(0.0))), 1, Stheno.GPC(2)), Af = Stheno.CompositeGP{Tuple{typeof(Main.##306.A), Stheno.WrappedGP{AbstractGPs.GP{AbstractGPs.ZeroMean{Float64}, KernelFunctions.SqExponentialKernel{Distances.Euclidean}}}}}((Main.##306.A, Stheno.WrappedGP{AbstractGPs.GP{AbstractGPs.ZeroMean{Float64}, KernelFunctions.SqExponentialKernel{Distances.Euclidean}}}(AbstractGPs.GP{AbstractGPs.ZeroMean{Float64}, KernelFunctions.SqExponentialKernel{Distances.Euclidean}}(AbstractGPs.ZeroMean{Float64}(), Squared Exponential Kernel (metric = Distances.Euclidean(0.0))), 1, Stheno.GPC(2))), 2, Stheno.GPC(2))), Stheno.GPC(2))"
     },
     "metadata": {},
     "execution_count": 7
    }
   ],
   "cell_type": "code",
   "source": [
    "gppp = @gppp let\n",
    "    f = GP(SEKernel())\n",
    "    Af = A(f)\n",
    "end"
   ],
   "metadata": {},
   "execution_count": 7
  },
  {
   "cell_type": "markdown",
   "source": [
    "Also create some input vectors."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "3-element Stheno.GPPPInput{Symbol, Float64, Vector{Float64}}:\n (:Af, 0.47275202777517794)\n (:Af, -2.140517805875411)\n (:Af, -0.7388864589574182)"
     },
     "metadata": {},
     "execution_count": 8
    }
   ],
   "cell_type": "code",
   "source": [
    "x_f = GPPPInput(:f, randn(3))\n",
    "y_f = GPPPInput(:f, randn(6))\n",
    "x_Af = GPPPInput(:Af, randn(3))\n",
    "y_Af = GPPPInput(:Af, randn(6))\n",
    "z_Af = GPPPInput(:Af, randn(3))"
   ],
   "metadata": {},
   "execution_count": 8
  },
  {
   "cell_type": "markdown",
   "source": [
    "The covariance matrix at a single pair of inputs:"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "true"
     },
     "metadata": {},
     "execution_count": 9
    }
   ],
   "cell_type": "code",
   "source": [
    "function Stheno.cov((A, f)::A_args, x::AbstractVector)\n",
    "    return cov(f, x) + cov(f, x, x .+ 3) + cov(f, x .+ 3, x) + cov(f, x .+ 3)\n",
    "end\n",
    "\n",
    "cov(gppp, x_Af, x_Af) ≈ cov(gppp, x_Af)"
   ],
   "metadata": {},
   "execution_count": 9
  },
  {
   "cell_type": "markdown",
   "source": [
    "The diagonal of the covariance matrix at a single pair of inputs:"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "true"
     },
     "metadata": {},
     "execution_count": 10
    }
   ],
   "cell_type": "code",
   "source": [
    "function Stheno.var((A, f)::A_args, x::AbstractVector)\n",
    "    return var(f, x) + var(f, x .+ 3) + var(f, x, x .+ 3) + var(f, x .+ 3, x)\n",
    "end\n",
    "\n",
    "var(gppp, x_Af) ≈ diag(cov(gppp, x_Af))"
   ],
   "metadata": {},
   "execution_count": 10
  },
  {
   "cell_type": "markdown",
   "source": [
    "The diagonal of the cross-covariance matrix for equal-length inputs:"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "true"
     },
     "metadata": {},
     "execution_count": 11
    }
   ],
   "cell_type": "code",
   "source": [
    "function Stheno.var((A, f)::A_args, x::AbstractVector, y::AbstractVector)\n",
    "    return var(f, x, y) + var(f, x, y .+ 3) + var(f, x .+ 3, y) + var(f, x .+ 3, y .+ 3)\n",
    "end\n",
    "\n",
    "var(gppp, x_Af, z_Af) ≈ diag(cov(gppp, x_Af, z_Af))"
   ],
   "metadata": {},
   "execution_count": 11
  },
  {
   "cell_type": "markdown",
   "source": [
    "The diagonal of the cross-covariance between different processes for equal-length inputs:"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "true"
     },
     "metadata": {},
     "execution_count": 12
    }
   ],
   "cell_type": "code",
   "source": [
    "function Stheno.var((A, f)::A_args, g::AbstractGP, x::AbstractVector, y::AbstractVector)\n",
    "    return var(f, g, x, y) + var(f, g, x .+ 3, y)\n",
    "end\n",
    "\n",
    "var(gppp, x_Af, x_f) ≈ diag(cov(gppp, x_Af, x_f))"
   ],
   "metadata": {},
   "execution_count": 12
  },
  {
   "cell_type": "markdown",
   "source": [
    "`cov` and `var` between processes when `Af`'s arguments are the second argument, rather\n",
    "than the first:"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "true"
     },
     "metadata": {},
     "execution_count": 13
    }
   ],
   "cell_type": "code",
   "source": [
    "function Stheno.cov(g::AbstractGP, (A, f)::A_args, x::AbstractVector, y::AbstractVector)\n",
    "    return cov(g, f, x, y) + cov(g, f, x, y .+ 3)\n",
    "end\n",
    "\n",
    "cov(gppp, x_f, x_Af) ≈ cov(gppp, x_Af, x_f)'\n",
    "\n",
    "function Stheno.var(g::AbstractGP, (A, f)::A_args, x::AbstractVector, y::AbstractVector)\n",
    "    return var(g, f, x, y) + var(g, f, x, y .+ 3)\n",
    "end\n",
    "\n",
    "var(gppp, x_f, x_Af) ≈ var(gppp, x_Af, x_f)"
   ],
   "metadata": {},
   "execution_count": 13
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Checking Your Implementation"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "Given the numerous methods above, it's a really good idea to utilise the functionality\n",
    "provided by AbstractGPs.jl to check that you've implemented them all consistently with one\n",
    "another."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using AbstractGPs.TestUtils: test_internal_abstractgps_interface\n",
    "using Random\n",
    "\n",
    "rng = MersenneTwister(123456);\n",
    "test_internal_abstractgps_interface(rng, gppp, x_Af, y_Af);\n",
    "test_internal_abstractgps_interface(rng, gppp, x_Af, y_f);\n",
    "test_internal_abstractgps_interface(rng, gppp, x_f, y_Af);"
   ],
   "metadata": {},
   "execution_count": 14
  },
  {
   "cell_type": "markdown",
   "source": [
    "Roughly speaking, provided that you've implemented the first three methods correctly, this\n",
    "test ought to catch any glaring problems if you've made a mistake in the rest.\n",
    "If course, it won't check that your implementations of the first three methods correctly\n",
    "implement the desired affine transformation, so you should write whatever tests you need\n",
    "in order to convince yourself of that."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.1"
  },
  "kernelspec": {
   "name": "julia-1.7",
   "display_name": "Julia 1.7.1",
   "language": "julia"
  }
 },
 "nbformat": 4
}
